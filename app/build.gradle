repositories {
    google()
    mavenCentral()
    maven { url 'https://jitpack.io' }
}

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'

def acraUri = ""
def acraLogin = ""
def acraPassword = ""

def financierProperties = new Properties()
try {
    def financierPropertiesFile = rootProject.file("./Keys/financier.properties")
    financierProperties.load(new FileInputStream(financierPropertiesFile))
    acraUri = financierProperties['acraUri']
    acraLogin = financierProperties['acraLogin']
    acraPassword = financierProperties['acraPassword']
} catch(Exception ignored) {
}

android {
    buildFeatures {
        dataBinding true
        viewBinding true
    }
    compileSdk 34

    flavorDimensions "channel"
    productFlavors {
        untied {
            dimension "channel"
        }
        googleplay {
            dimension "channel"
        }
        fdroid {
            dimension "channel"
        }
    }

    signingConfigs {
        release {
            try {
                storeFile file(financierProperties['storeFile'])
                storePassword financierProperties['storePassword']
                keyAlias financierProperties['keyAlias']
                keyPassword financierProperties['keyPassword']
            } catch(Exception ignored) {
            }
        }
    }

    defaultConfig {
        applicationId "com.handydev.financier"
        minSdkVersion 21
        targetSdkVersion 34
        versionCode 226
        versionName "2.0.25"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        multiDexEnabled true
        buildConfigField "String", "ACRA_URI", "\"$acraUri\""
        buildConfigField "String", "ACRA_LOGIN", "\"$acraLogin\""
        buildConfigField "String", "ACRA_PASSWORD", "\"$acraPassword\""
    }

    buildTypes {
        release {
            minifyEnabled false
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            applicationVariants.all { variant ->
                variant.outputs.all { output ->
                    //def appId = variant.applicationId// com.exampleFree.app OR com.examplePaid.app
                    def versionName = variant.versionName
                    //def versionCode = variant.versionCode // e.g 1.0
                    //def flavorName = variant.flavorName // e. g. Free
                    //def buildType = variant.buildType.name // e. g. debug
                    def variantName = variant.name // e. g. FreeDebug
                    if(variant.flavorName == "untied") {
                        outputFileName = "Financier.apk"
                    } else {
                        //customize your app name by using variables
                        outputFileName = "Financier_${variantName}_${versionName}.apk"
                    }
                }
            }
        }
    }

    dexOptions {
        javaMaxHeapSize "2g"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }


    testOptions {
        unitTests {
            returnDefaultValues = true
            includeAndroidResources = true
        }
    }

    sourceSets {
        test {
            resources.srcDirs += ['src/test/resources']
        }
    }
    packagingOptions {
        resources {
            excludes += ['META-INF/DEPENDENCIES.txt', 'META-INF/LICENSE', 'META-INF/LICENSE.txt', 'META-INF/NOTICE.txt', 'META-INF/NOTICE', 'META-INF/LICENSE', 'META-INF/DEPENDENCIES', 'META-INF/notice.txt', 'META-INF/license.txt', 'META-INF/dependencies.txt', 'META-INF/LGPL2.1']
        }
    }

    namespace 'com.handydev.financier'
    lint {
        abortOnError false
        checkReleaseBuilds false
    }
}

def AAVersion = '4.7.0'
def eventbus_version = '3.3.1'
def acraVersion = "5.8.4"

dependencies {
    implementation("ch.acra:acra-http:$acraVersion")
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'androidx.multidex:multidex:2.0.1'
    implementation "androidx.constraintlayout:constraintlayout:2.1.4"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycle_version"
    implementation "androidx.viewpager2:viewpager2:1.0.0"
    implementation "androidx.lifecycle:lifecycle-common-java8:$lifecycle_version"
    implementation "androidx.preference:preference-ktx:1.2.1"

    //googledrive
    googleplayImplementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.6.4'
    googleplayImplementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.6.4'
    googleplayImplementation 'com.google.android.gms:play-services-auth:20.7.0'
    googleplayImplementation 'com.google.http-client:google-http-client-gson:1.26.0'
    googleplayImplementation 'com.google.api-client:google-api-client-android:1.26.0'
    googleplayImplementation 'com.google.apis:google-api-services-drive:v3-rev136-1.25.0'
        //googledrive misc
    googleplayImplementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.6.2'
    googleplayImplementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'
    googleplayImplementation ('androidx.work:work-runtime-ktx:2.8.1'){
        //Needed due to a transitive dependency clash with the google api client dep
        exclude group: 'com.google.guava', module:'listenablefuture'
    }
    //googledrive

    //untied - SAME AS ABOVE
    untiedImplementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.6.4'
    untiedImplementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.6.4'
    untiedImplementation 'com.google.android.gms:play-services-auth:20.7.0'
    untiedImplementation 'com.google.http-client:google-http-client-gson:1.26.0'
    untiedImplementation 'com.google.api-client:google-api-client-android:1.26.0'
    untiedImplementation 'com.google.apis:google-api-services-drive:v3-rev136-1.25.0'
    //googledrive misc
    untiedImplementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.6.2'
    untiedImplementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'
    untiedImplementation ('androidx.work:work-runtime-ktx:2.8.1'){
        //Needed due to a transitive dependency clash with the google api client dep
        exclude group: 'com.google.guava', module:'listenablefuture'
    }
    //googledrive

    kapt "org.androidannotations:androidannotations:$AAVersion"
    annotationProcessor "org.androidannotations:androidannotations:$AAVersion"
    implementation "org.androidannotations:androidannotations-api:$AAVersion"

    implementation 'com.google.code.gson:gson:2.8.9'
    implementation 'net.sf.trove4j:trove4j:3.0.3'

    implementation 'com.dropbox.core:dropbox-core-sdk:3.1.3'
    implementation 'com.squareup.okhttp3:okhttp:4.9.1'
    implementation 'com.squareup.okio:okio:2.9.0'
    implementation "org.greenrobot:eventbus:$eventbus_version"
    kapt "org.greenrobot:eventbus-annotation-processor:$eventbus_version"
    implementation 'com.wdullaer:materialdatetimepicker:3.6.4'
    implementation 'commons-io:commons-io:2.6'
    implementation 'com.mtramin:rxfingerprint:2.2.1'
    implementation 'io.reactivex.rxjava2:rxjava:2.2.21'
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.1'
    implementation 'org.reactivestreams:reactive-streams:1.0.3'
    implementation 'com.mlsdev.rximagepicker:library:2.1.5'
    implementation 'com.github.bumptech.glide:glide:4.12.0'
    implementation 'com.google.android.material:material:1.10.0'

    implementation fileTree(include: '**/*.jar', dir: 'libs')

    testImplementation 'junit:junit:4.13.2'
    testImplementation 'androidx.test:core:1.5.0'
    testImplementation 'org.robolectric:robolectric:4.6.1'
    implementation "androidx.core:core-ktx:1.12.0"
}

kapt {
    arguments {
        arg('eventBusIndex', 'com.handydev.financier.MyEventBusIndex')
    }
}